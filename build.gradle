plugins {
    id 'java'
    id 'org.ajoberstar.grgit' version '5.2.1'
}

boolean isSnapshot = System.getenv("RELEASE") != 'true';

allprojects {
    apply plugin: 'idea'
    apply plugin: 'eclipse'

    group = "${project_group}"
    try {
        version = isSnapshot ? grgit.head().id.subSequence(0, 5) : grgit.tag.list().get(0).getName();
    } catch (IndexOutOfBoundsException e) {
        version = "undefined"
    }

    eclipse {
        classpath {
            downloadSources = true
        }
    }

    idea {
        module {
            downloadSources = true
        }
    }

    compileJava {
        options.encoding = "UTF-8"
    }

    repositories {
        mavenCentral()
        maven {
            url "https://repository.atlasworld.fr/repository/maven-public/"
        }
    }

    java {
        toolchain.languageVersion.set(JavaLanguageVersion.of(project.java_version))

        withJavadocJar()
        withSourcesJar()
    }
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'

    publishing {
        repositories {
            maven {
                name 'AtlasWorld-Repo'

                def release = "https://repository.atlasworld.fr/repository/maven-release/"
                def snapshot = "https://repository.atlasworld.fr/repository/maven-snapshot/"
                url = isSnapshot ? snapshot : release

                credentials {
                    username = System.getenv("AW_REPO_USERNAME")
                    password = System.getenv("AW_REPO_PASSWORD")
                }

                metadataSources {
                    gradleMetadata()
                }
            }
        }
        publications {
            AtlasCommon(MavenPublication) {
                from components.java

                pom {
                    name = project.name
                    description = project.description
                    licenses {
                        license {
                            name = 'GNU-GPL-v2.0'
                            url = 'https://github.com/AtlasWorldMC/EventFlow/blob/master/LICENSE'
                        }
                    }
                }
            }
        }
    }

    java {
        withJavadocJar()
        withSourcesJar()
    }
}